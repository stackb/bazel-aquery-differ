load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load(":platforms.bzl", "platforms")

go_library(
    name = "aquerydiff_lib",
    srcs = [
        "config.go",
        "main.go",
    ],
    importpath = "github.com/stackb/bazel-aquery-differ/cmd/aquerydiff",
    visibility = ["//visibility:private"],
    deps = [
        "//pkg/action",
        "//pkg/protobuf",
        "@bazelapis//src/main/protobuf:analysis_v2_go_proto",
    ],
)

go_binary(
    name = "aquerydiff",
    embed = [":aquerydiff_lib"],
    visibility = ["//visibility:public"],
)

[
    go_binary(
        name = "aquerydiff_%s_%s" % (
            platform.os,
            platform.arch,
        ),
        embed = [":aquerydiff_lib"],
        gc_linkopts = platform.gc_linkopts,
        goarch = platform.arch,
        goos = platform.os,
        pure = "on",
        static = "on",
        visibility = ["//visibility:public"],
    )
    for platform in platforms
]

[
    genrule(
        name = "aquerydiff_%s_%s_zip" % (
            platform.os,
            platform.arch,
        ),
        srcs = ["aquerydiff_%s_%s" % (
            platform.os,
            platform.arch,
        )],
        outs = ["aquerydiff_%s_%s.zip" % (
            platform.os,
            platform.arch,
        )],
        cmd = """
    cp $(location aquerydiff_{os}_{arch}) aquerydiff
    $(location @bazel_tools//tools/zip:zipper) c $@ aquerydiff
    """.format(
            arch = platform.arch,
            os = platform.os,
        ),
        tools = ["@bazel_tools//tools/zip:zipper"],
        visibility = ["//visibility:public"],
    )
    for platform in platforms
]
